//=============================================================================
// Global initializer - not real quest
// 1. set start date
// 2. generate corner dungeon with gladiator armor
// 3. create secret vault
//=============================================================================
quest global_init
{
	type unique
	flags dont_count
	
	code {
		Location@ artifacts_loc;
		Location@ vault;
		
		void Startup()
		{
			World::startDate = Date(631, Rand() % 6, Rand() % 20);
			CreateArtifactLocation();
			CreateVault();
		}
		
		void CreateArtifactLocation()
		{
			Vec2 bounds = World::bounds;
			for(int i = 0; i < 10; ++i)
			{
				Vec2 pos;
				switch(Rand() % 4)
				{
				case 0:
					pos = Vec2(bounds.x + 16.f, bounds.x + 16.f);
					break;
				case 1:
					pos = Vec2(bounds.y - 16.f, bounds.x + 16.f);
					break;
				case 2:
					pos = Vec2(bounds.x + 16.f, bounds.y - 16.f);
					break;
				case 3:
					pos = Vec2(bounds.y - 16.f, bounds.y - 16.f);
					break;
				}
				if(!World::TryFindPlace(pos, 64.f))
					continue;
				@artifacts_loc = World::CreateLocation(L_DUNGEON, pos, HERO_CRYPT, 2);
				if(artifacts_loc !is null)
					break;
			}
			@artifacts_loc.group = UnitGroup::Get("golems");
			@artifacts_loc.active_quest = quest;
			artifacts_loc.st = 12;
			artifacts_loc.AddEventHandler(quest, EVENT_ENTER);
			
			DevInfo(Format("Gladiator armor - {0}.", artifacts_loc.name));
		}
		
		void CreateVault()
		{
			@vault = World::CreateLocation(L_CAVE, World::GetRandomPlace());
			@vault.active_quest = quest;
			vault.st = 6;
			vault.AddEventHandler(quest, EVENT_ENTER);
			
			DevInfo(Format("Vault - {0}.", vault.name));
		}
		
		void OnEvent(Event@ e)
		{
			if(e.event != EVENT_ENTER)
				return;
			if(Level::location is artifacts_loc)
			{
				if(Level::dungeon_level == 1)
				{
					e.location.RemoveEventHandler(quest);
					Room@ room = Level::GetRoom(ROOM_TREASURY);
					Chest@ chest = Level::GetRandomChest(room);
					chest.AddItem(Item::Get("am_gladiator"));
					Level::SpawnUnit(room, UnitData::Get("golem_adamantine"));
					Level::SpawnUnit(room, UnitData::Get("golem_iron"));
				}
			}
			else if(Level::location is vault)
			{
				e.location.RemoveEventHandler(quest);
				BaseObject@ obj = BaseObject::Get("vault_door");
				SpawnPoint point;
				if(Level::FindPlaceNearWall(obj, point))
				{
					Level::SpawnObject(obj, point.pos, point.rot);
					UnitData@ skeleton = UnitData::Get("skeleton");
					for(int i=0; i<3; ++i)
					{
						Unit@ unit = Level::SpawnUnitNearLocation(skeleton, point.pos, 6);
						if(unit !is null)
						{
							if(i == 0)
								unit.AddItem(Item::Get("crypt_note"));
							unit.Kill();
						}
					}
				}
			}
		}
		
		void SetProgress()
		{
		}
		
		void OnUpgrade(Vars@ vars)
		{
			@artifacts_loc = World::GetLocation(int(vars["artifacts_loc"]));
			@artifacts_loc.active_quest = quest;
			if(!bool(vars["done"]))
				artifacts_loc.AddEventHandler(quest, EVENT_ENTER);
		}
	}
}

//=============================================================================
// DELIVER LETTER - random mayor quest
//=============================================================================
quest deliver_letter
{
	type mayor
	progress {
		P_LETTER_NONE
		P_LETTER_STARTED
		P_LETTER_TIMEOUT
		P_LETTER_GOT_RESPONSE
		P_LETTER_FINISHED
		P_LETTER_ON_TIMEOUT
	}

	dialog start {
		talk 0
		choice 1
		{
			talk 2
			talk 3
			set_quest_progress P_LETTER_STARTED
			end
		}
		escape choice 4
			end
		show_choices
	}

	dialog target {
		if have_quest_item
			choice 0
			{
				talk 1
				talk 2
				if quest_progress == P_LETTER_ON_TIMEOUT
				{
					talk 3
					set_quest_progress P_LETTER_TIMEOUT
					end
				}
				talk 4
				talk 5
				set_quest_progress P_LETTER_GOT_RESPONSE
				end
			}
		end
	}

	dialog end {
		if quest_progress == P_LETTER_ON_TIMEOUT
		{
			do_once
			talk 0
			set_quest_progress P_LETTER_TIMEOUT
			end
		}
		else if quest_progress == P_LETTER_GOT_RESPONSE
			 if have_quest_item
		{
			choice 1
			{
				talk 2
				talk 3
				talk 4
				set_quest_progress P_LETTER_FINISHED
				end
			}
		}
		end
	}

	code {
		Location@ start_loc;
		Location@ end_loc;
		Item@ letter;

		void Startup()
		{
			@start_loc = Level::location;
			@end_loc = World::GetRandomSettlement(start_loc);
		}

		void SetProgress()
		{
			switch(progress)
			{
			case P_LETTER_STARTED:
				{
					quest.SetStarted(TEXT(0));
					quest.AddEntry(TEXT(1));
					quest.AddEntry(TEXT(2));
					quest.SetTimeout(30);

					Item@ item = Item::Get("letter");
					@letter = item.QuestCopy(quest, TEXT(3));
					pc.unit.AddTeamItem(letter);

					target.AddDialog(quest, "end");
					end_loc.AddEventHandler(quest, EVENT_ENTER);
				}
				break;
			case P_LETTER_TIMEOUT:
				{
					quest.AddEntry(TEXT(5));
					quest.SetFailed();
					if(Level::location is end_loc)
						pc.unit.RemoveQuestItem(quest);
				}
				break;
			case P_LETTER_GOT_RESPONSE:
				{
					letter.name = TEXT(4);
					quest.AddEntry(TEXT(6));
				}
				break;
			case P_LETTER_FINISHED:
				{
					quest.AddEntry(TEXT(7));
					quest.SetCompleted();

					Team::AddReward(250, 1000);
					pc.unit.RemoveQuestItem(quest);
				}
				break;
			}
		}

		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_TIMEOUT)
			{
				quest.AddEntry(TEXT(8));
				progress = P_LETTER_ON_TIMEOUT;
			}
			else if(e.event == EVENT_ENTER)
			{
				e.location.RemoveEventHandler(quest);
				Unit@ mayor = Level::GetMayor();
				mayor.AddDialog(quest, "target");
			}
		}

		void OnUpgrade(Vars@ vars)
		{
			@start_loc = World::GetLocation(int(vars["start_loc"]));
			@end_loc = World::GetLocation(int(vars["end_loc"]));
			int start_time = int(vars["start_time"]);

			if(quest.state != Q_HIDDEN)
			{
				Item@ item = Item::Get("letter");
				@letter = item.QuestCopy(quest, TEXT(progress >= P_LETTER_GOT_RESPONSE ? 4 : 3));
			}

			if(quest.state != Q_STARTED)
				return;

			int days = 30 + start_time - World::worldtime;
			if(days > 0)
				quest.SetTimeout(days);
			else if(progress == P_LETTER_STARTED)
				progress = P_LETTER_ON_TIMEOUT;

			if(end_loc.visited)
			{
				Unit@ mayor = end_loc.GetMayor();
				mayor.AddDialog(quest, "target");
			}
			else
				end_loc.AddEventHandler(quest, EVENT_ENTER);

			Unit@ mayor = start_loc.GetMayor();
			mayor.AddDialog(quest, "end");
		}
	}
}

//=============================================================================
// DELIVER RESOURCES - random mayor quest
//=============================================================================
quest deliver_resources
{
	type mayor
	progress {
		P_DELIVER_NONE
		P_DELIVER_STARTED
		P_DELIVER_ENCOUNTER
		P_DELIVER_TALKED
		P_DELIVER_TIMEOUT
		P_DELIVER_FINISHED
	}

	dialog start {
		talk 0
		talk 1
		choice 2
		{
			set_quest_progress P_DELIVER_STARTED
			talk 4
			end
		}
		escape choice 3
			end
		show_choices
	}

	dialog end {
		if quest_progress == P_DELIVER_STARTED
		{
			do_once
			talk 2
			if not have_quest_item
				talk 3
			set_quest_progress P_DELIVER_TALKED
		}
		if have_quest_item
			choice 0
			{
				set_quest_progress P_DELIVER_FINISHED
				talk 1
				end
			}
		end
	}

	dialog bandits_enc {
		talk 0
		script "target.OrderAttack()"
		end
	}

	dialog bandits {
		talk 0
		script "target.OrderAttack()"
		end2
	}

	code {
		Location@ start_loc;
		Location@ target_loc;

		void Startup()
		{
			@start_loc = Level::location;
			@target_loc = GetTargetLocation();
			@target_loc.active_quest = quest;
		}

		// get random village that is between 8 or 14 days of travel
		// or farest if not available
		Location@ GetTargetLocation()
		{
			uint count = World::GetSettlements();
			uint index = Rand() % count;
			uint start_index = index;
			int best_index = -1;
			float best_dist = 0;
			while(true)
			{
				Location@ loc = World::GetLocation(index);
				if(loc.IsVillage() && loc !is start_loc && loc.active_quest is null)
				{
					float dist = Vec2::Distance(start_loc.pos, loc.pos);
					float days = World::GetTravelDays(dist);
					if(days >= 8 && days < 14)
						return loc;
					if(days < 10 && (best_index == -1 || best_dist < dist))
					{
						best_index = index;
						best_dist = dist;
					}
				}
				index = (index + 1) % count;
				if(index == start_index)
					break;
			}
			return World::GetLocation(best_index);
		}

		void SetProgress()
		{
			switch(progress)
			{
			case P_DELIVER_STARTED:
				{
					Item@ item = Item::Get("q_resources");
					@item = item.QuestCopy(quest, TEXT(0));
					pc.unit.AddTeamItem(item);
					target_loc.AddEventHandler(quest, EVENT_ENTER);
					quest.SetStarted(TEXT(1));
					quest.AddEntry(TEXT(2));
					quest.AddEntry(TEXT(3));
					quest.SetTimeout(15);
					if(Rand() % 4 != 0)
					{
						Encounter@ enc = World::AddEncounter(quest);
						enc.pos = (start_loc.pos + target_loc.pos) / 2;
						enc.dont_attack = true;
						enc.text = TEXT(6);
						@enc.group = UnitGroup::Get("bandits");
						@enc.dialog = quest.GetDialog("bandits_enc");
						@enc.quest = quest;
						enc.st = 8;
					}
				}
				break;
			case P_DELIVER_TALKED:
				target.dont_attack = false;
				break;
			case P_DELIVER_FINISHED:
				quest.AddEntry(TEXT(4));
				quest.SetCompleted();
				Team::AddReward(1500, 3000);
				pc.unit.RemoveQuestItem(quest);
				@target_loc.active_quest = null;
				break;
			}
		}

		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_ENTER)
			{
				e.location.RemoveEventHandler(quest);
				Unit@ mayor = Level::GetMayor();
				mayor.AddDialog(quest, "end");
				if(progress != P_DELIVER_ENCOUNTER)
				{
					World::RemoveEncounter(quest);
					mayor.dont_attack = true;
					SpawnGroup group;
					group.Fill(UnitGroup::Get("bandits"), 2, 8);
					bool first = true;
					for(uint i=0; i<group.count; ++i)
					{
						Unit@ unit = Level::SpawnUnit(mayor.area, group.Get(i));
						if(unit !is null)
						{
							unit.dont_attack = true;
							if(first)
							{
								first = false;
								unit.OrderAutoTalk();
								unit.AddDialog(quest, "bandits");
							}
						}
					}
				}
			}
			else if(e.event == EVENT_TIMEOUT)
			{
				quest.AddEntry(TEXT(5));
				quest.SetFailed();
				progress = P_DELIVER_TIMEOUT;
				@target_loc.active_quest = null;
			}
			else if(e.event == EVENT_ENCOUNTER)
			{
				quest.AddEntry(TEXT(7));
				progress = P_DELIVER_ENCOUNTER;
			}
		}
	}
}

//=============================================================================
// BANDITS COLLECT TOLL - random captain quest
//=============================================================================
quest bandits_collect_toll
{
	type captain
	progress {
		P_TOLL_NONE
		P_TOLL_STARTED
		P_TOLL_TIMEOUT
		P_TOLL_KILLED_BANDITS
		P_TOLL_FINISHED
		P_TOLL_ON_TIMEOUT
	}

	dialog start {
		talk 0
		talk 1
		choice 2
		{
			set_quest_progress P_TOLL_STARTED
			talk 3
			talk 4
			end
		}
		escape choice 5
			end
		show_choices
	}

	dialog end {
		if quest_progress == P_TOLL_ON_TIMEOUT
		{
			do_once
			set_quest_progress P_TOLL_TIMEOUT
			talk 0
			talk 1
			end
		}
		else if quest_progress == P_TOLL_KILLED_BANDITS
		{
			do_once
			set_quest_progress P_TOLL_FINISHED
			talk 2
			talk 3
			end
		}
		end
	}

	dialog encounter {
		talk 0
		talk 1
		choice 2
		{
			if script "pc.unit.gold >= 500"
			{
				talk 3
				script "pc.unit.gold -= 500; target.gold += 500;"
				end2
			}
			else
			{
				talk 4
				talk 5
				script "target.OrderAttack()"
				end2
			}
		}
		choice 6
		{
			talk 7
			script "target.OrderAttack()"
			end2
		}
		show_choices
	}

	code {
		Location@ start_loc;
		Location@ other_loc;

		void Startup()
		{
			@start_loc = Level::location;
			@other_loc = World::GetRandomSettlement(start_loc);
		}

		void SetProgress()
		{
			switch(progress)
			{
			case P_TOLL_STARTED:
				{
					quest.SetStarted(TEXT(0));
					quest.AddEntry(TEXT(1));
					quest.AddEntry(TEXT(2));
					quest.SetTimeout(15);

					Encounter@ enc = World::AddEncounter(quest);
					enc.pos = (start_loc.pos + other_loc.pos) / 2;
					enc.dont_attack = true;
					enc.text = TEXT(3);
					@enc.group = UnitGroup::Get("bandits");
					@enc.dialog = quest.GetDialog("encounter");
					@enc.quest = quest;
					enc.st = 6;
					enc.chance = 50;

					target.AddDialog(quest, "end");
				}
				break;
			case P_TOLL_ON_TIMEOUT:
				quest.AddEntry(TEXT(8));
				break;
			case P_TOLL_TIMEOUT:
				quest.AddEntry(TEXT(4));
				quest.SetFailed();
				break;
			case P_TOLL_KILLED_BANDITS:
				quest.AddEntry(TEXT(5));
				break;
			case P_TOLL_FINISHED:
				quest.AddEntry(TEXT(6));
				quest.SetCompleted();
				Team::AddReward(1500, 4500);
				World::AddNews(TEXT(7));
				break;
			}
		}

		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_TIMEOUT)
			{
				if(progress != P_TOLL_KILLED_BANDITS)
					progress = P_TOLL_ON_TIMEOUT;
			}
			else if(e.event == EVENT_ENCOUNTER)
				Level::location.AddEventHandler(quest, EVENT_CLEARED);
			else if(e.event == EVENT_CLEARED)
				progress = P_TOLL_KILLED_BANDITS;
		}

		void OnUpgrade(Vars@ vars)
		{
			@start_loc = World::GetLocation(int(vars["start_loc"]));
			@other_loc = World::GetLocation(int(vars["other_loc"]));
			int start_time = int(vars["start_time"]);
			int enc_index = int(vars["enc"]);
			bool in_enc = bool(vars["in_enc"]);

			if(quest.state != Q_STARTED)
				return;

			int days = 15 + start_time - World::worldtime;
			if(days > 0)
				quest.SetTimeout(days);
			else if(progress == P_TOLL_STARTED)
				progress = P_TOLL_ON_TIMEOUT;

			if(enc_index != -1)
			{
				Encounter@ enc = World::RecreateEncounter(quest, enc_index);
				enc.pos = (start_loc.pos + other_loc.pos) / 2;
				enc.dont_attack = true;
				enc.text = TEXT(3);
				@enc.group = UnitGroup::Get("bandits");
				@enc.dialog = quest.GetDialog("encounter");
				@enc.quest = quest;
				enc.st = 6;
				enc.chance = 50;
			}

			if(in_enc)
				Level::location.AddEventHandler(quest, EVENT_CLEARED);

			start_loc.GetCaptain().AddDialog(quest, "end");
		}
	}
}

//=============================================================================
// CAMP NEAR CITY - random captain quest
// Bandits have camp near city, need to clean it up.
//=============================================================================
quest camp_near_city
{
	type captain
	progress {
		P_CAMP_NONE
		P_CAMP_STARTED
		P_CAMP_CLEARED
		P_CAMP_FINISHED
		P_CAMP_TIMEOUT
		P_CAMP_ON_TIMEOUT
	}
	
	dialog start {
		talk 0
		talk 1
		talk 2
		choice 3
		{
			set_quest_progress P_CAMP_STARTED
			talk 4
			talk 5
			end
		}
		escape choice 6
			end
		show_choices
	}
	
	dialog end {
		if quest_progress == P_CAMP_CLEARED
		{
			do_once
			set_quest_progress P_CAMP_FINISHED
			talk 0
			talk 1
		}
		else if quest_progress == P_CAMP_ON_TIMEOUT
		{
			do_once
			set_quest_progress P_CAMP_TIMEOUT
			talk 2
			talk 3
		}
		end
	}
	
	code {
		Location@ start_loc;
		Location@ camp;
		UnitGroup@ group;
		int st;
		
		void Startup()
		{
			@start_loc = Level::location;
			switch(Rand() % 3)
			{
			case 0:
				@group = UnitGroup::Get("bandits");
				break;
			case 1:
				@group = UnitGroup::Get("goblins");
				break;
			case 2:
				@group = UnitGroup::Get("orcs");
				break;
			}
		}
		
		void SetProgress()
		{
			switch(progress)
			{
			case P_CAMP_STARTED:
				{
					@camp = World::CreateCamp(World::FindPlace(start_loc.pos, 64.f, false), group);
					@camp.active_quest = quest;
					camp.SetKnown();
					camp.st += Random(3, 5);
					camp.AddEventHandler(quest, EVENT_CLEARED);
					st = camp.st;
					
					target.AddDialog(quest, "end");
					
					quest.SetStarted(TEXT(0));
					quest.AddEntry(TEXT(1));
					quest.AddEntry(TEXT(2));
					quest.SetTimeout(30);
				}
				break;
			case P_CAMP_CLEARED:
				quest.AddEntry(TEXT(3));
				camp.RemoveEventHandler(quest);
				@camp.active_quest = null;
				break;
			case P_CAMP_FINISHED:
				{
					quest.AddEntry(TEXT(4));
					quest.SetCompleted();
					int reward = GetReward();
					Team::AddReward(reward, reward * 3);
				}
				break;
			case P_CAMP_ON_TIMEOUT:
				quest.AddEntry(TEXT(5));
				World::AbadonLocation(camp);
				camp.RemoveEventHandler(quest);
				@camp.active_quest = null;
				break;
			case P_CAMP_TIMEOUT:
				quest.AddEntry(TEXT(6));
				quest.SetFailed();
				break;
			}
		}
		
		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_CLEARED)
				progress = P_CAMP_CLEARED;
			else if(e.event == EVENT_TIMEOUT)
			{
				if(progress == P_CAMP_STARTED)
					progress = P_CAMP_ON_TIMEOUT;
			}
		}
		
		int GetReward()
		{
			return Quest::CalculateReward(st, Int2(5, 15), Int2(2500, 5000));
		}
		
		void OnUpgrade(Vars@ vars)
		{
			@start_loc = vars["start_loc"];
			@camp = vars["target_loc"];
			int start_time = vars["start_time"];
			@group = vars["group"];
			
			if(quest.state != Q_STARTED)
				return;
			
			start_loc.GetCaptain().AddDialog(quest, "end");
			
			int days = 30 + start_time - World::worldtime;
			if(days > 0)
				quest.SetTimeout(days);
			else if(progress == P_CAMP_STARTED)
				progress = P_CAMP_ON_TIMEOUT;
			
			if(progress == P_CAMP_STARTED)
			{
				@camp.active_quest = quest;
				camp.AddEventHandler(quest, EVENT_CLEARED);
			}
		}
	}
}

//=============================================================================
// WANTED - random captain quest
// There is wanted hero in one of cities, killing him will get reward
//=============================================================================
quest wanted
{
	type captain
	progress {
		P_WANTED_NONE
		P_WANTED_STARTED
		P_WANTED_TIMEOUT
		P_WANTED_KILLED
		P_WANTED_FINISHED
		P_WANTED_RECRUITED
		P_WANTED_ON_TIMEOUT
	}
	
	dialog start
	{
		talk 0
		talk 1
		talk 2
		choice 3
		{
			set_quest_progress P_WANTED_STARTED
			talk 4
			talk 5
			talk 6
			end
		}
		escape choice 7
			end
		show_choices
	}
	
	dialog wanted
	{
		if script "!target.IsTeamMember()"
		if have_quest_item
		{
			choice 0
			{
				talk 1
				special "force_attack"
				end
			}
		}
		end
	}
	
	dialog end
	{
		if quest_progress == P_WANTED_KILLED
		{
			talk 0
			set_quest_progress P_WANTED_FINISHED
			talk 1
		}
		else if quest_progress == P_WANTED_ON_TIMEOUT
		{
			set_quest_progress P_WANTED_TIMEOUT
			talk 2
			talk 3
		}
		end
	}
	
	code {
		Location@ startLoc;
		Location@ targetLoc;
		Class@ clas;
		int level;
		bool crazy;
		string name;
		Unit@ target;
		
		void Startup()
		{
			@startLoc = Level::location;
			level = Random(5, 15);
			crazy = Rand() % 5 == 0;
			@clas = crazy ? Class::GetRandomCrazy() : Class::GetRandomHero(true);
		}
		
		void SetProgress()
		{
			switch(progress)
			{
			case P_WANTED_STARTED:
				{
					name = clas.GenerateName(crazy);
					targetLoc = World::GetRandomNearSettlement();
					targetLoc.AddEventHandler(quest, EVENT_ENTER);

					Item@ item = Item::Get("wanted_letter").QuestCopy(quest, TEXT(3), TEXT(4));
					pc.unit.AddTeamItem(item);
					
					target.AddDialog(quest, "end");
					
					quest.SetStarted(TEXT(0));
					quest.AddEntry(TEXT(1));
					quest.AddEntry(TEXT(2));
					quest.SetTimeout(30);
				}
				break;
				
			case P_WANTED_KILLED:
				quest.Restart();
				quest.AddEntry(TEXT(5));
				break;
				
			case P_WANTED_FINISHED:
				quest.AddEntry(TEXT(6));
				quest.SetCompleted();
				Team::AddReward(level * 100, level * 250);
				break;
				
			case P_WANTED_ON_TIMEOUT:
				quest.AddEntry(TEXT(8));
				break;
				
			case P_WANTED_TIMEOUT:
				quest.AddEntry(TEXT(7));
				quest.SetFailed();
				break;
				
			case P_WANTED_RECRUITED:
				quest.SetFailed(false);
				quest.AddEntry(TEXT(8));
				break;
			}
		}
		
		void OnEvent(Event@ e)
		{
			switch(e.event)
			{
			case EVENT_ENTER:
				@target = Level::SpawnUnitInsideInn(crazy ? clas.crazy : clas.hero, level);
				target.dont_attack = true;
				target.active_quest = quest;
				target.hero.name = name;
				target.AddEventHandler(quest, EVENT_DIE | EVENT_RECRUIT | EVENT_KICK | EVENT_LEAVE);
				target.AddDialog(quest, "target");
				break;
			case EVENT_DIE:
				if(!e.unit.IsTeamMember())
					progress = P_WANTED_KILLED;
				break;
			case EVENT_RECRUIT:
				if(progress != P_WANTED_RECRUITED)
					progress = P_WANTED_RECRUITED;
				break;
			case EVENT_KICK:
				e.unit.temporary = false;
				break;
			case EVENT_LEAVE:
				@target = null;
				break;
			case EVENT_TIMEOUT:
				if(progress == P_WANTED_STARTED)
				{
					progress = P_WANTED_ON_TIMEOUT;
					if(target is null)
						targetLoc.RemoveEventHandler(quest, EVENT_ENTER);
					else
					{
						target.Remove();
						@target = null;
					}
				}
				else if(progress == P_WANTED_RECRUITED)
					quest.SetFailed();
				break;
			}
		}
		
		void OnUpgrade(Vars@ vars)
		{
			/*@start_loc = vars["start_loc"];
			@camp = vars["target_loc"];
			int start_time = vars["start_time"];
			@group = vars["group"];
			
			if(quest.state != Q_STARTED)
				return;
			
			start_loc.GetCaptain().AddDialog(quest, "end");
			
			int days = 30 + start_time - World::worldtime;
			if(days > 0)
				quest.SetTimeout(days);
			else if(progress == P_CAMP_STARTED)
				progress = P_CAMP_ON_TIMEOUT;
			
			if(progress == P_CAMP_STARTED)
			{
				@camp.active_quest = quest;
				camp.AddEventHandler(quest, EVENT_CLEARED);
			}*/
		}
	}
}

//=============================================================================
// KILL ANIMALS - random captain quest
// Go to forest/cave and kill all animals.
//=============================================================================
quest kill_animals
{
	type captain
	flags not_scripted
	
	progress {
		P_KILLANI_NONE
		P_KILLANI_STARTED
		P_KILLANI_CLEARED
		P_KILLANI_FINISHED
		P_KILLANI_TIMEOUT
		P_KILLANI_ON_TIMEOUT
	}
	
	properties {
		Location@ targetLoc;
	}
	
	dialog start
	{
		talk 0
		talk 1
		choice 2
		{
			set_quest_progress P_KILLANI_STARTED
			talk 3
			talk 4
			end
		}
		escape choice 5
			end
		show_choices
	}
	
	dialog captain
	{
		if quest_progress == P_KILLANI_CLEARED
		{
			do_once
			set_quest_progress P_KILLANI_FINISHED
			talk 0
			talk 1
		}
		else if quest_progress == P_KILLANI_ON_TIMEOUT
		{
			do_once
			set_quest_progress P_KILLANI_TIMEOUT
			talk 2
		}
		end
	}
}

//=============================================================================
// FIND ARTIFACT - random traveler quest
// Go to crypt and recover artifact from treasure.
//=============================================================================
quest find_artifact
{
	type random
	progress {
		P_FINDART_NONE
		P_FINDART_STARTED
		P_FINDART_FINISHED
		P_FINDART_TIMEOUT
		P_FINDART_ON_TIMEOUT
	}
	
	dialog start
	{
		talk 0
		talk 1
		choice 2
		{
			set_quest_progress P_FINDART_STARTED
			talk 3
			talk 4
			end
		}
		escape choice 5
			end
		show_choices
	}
	
	dialog end
	{
		if have_quest_item
		{
			choice 0
			{
				set_quest_progress P_FINDART_FINISHED
				talk 1
				end2
			}
		}
		else if quest_progress == P_FINDART_ON_TIMEOUT
		{
			set_quest_progress P_FINDART_TIMEOUT
			talk 2
			talk 3
			talk 4
			end2
		}
		end
	}
	
	code
	{
		Location@ start_loc;
		Location@ target_loc;
		Item@ item;
		int st;
		bool spawned;
		
		void Startup()
		{
			@start_loc = Level::location;
			@item = ItemList::GetRandomItem("artifacts");
			spawned = false;
		}

		void SetProgress()
		{
			switch(progress)
			{
			case P_FINDART_STARTED:
				{
					@item = item.QuestCopy(quest);
					
					if(Rand() % 4 == 0)
						@target_loc = World::GetClosestLocation(L_DUNGEON, World::pos, LABYRINTH);
					else
						@target_loc = World::GetClosestLocation(L_DUNGEON, World::pos, { HERO_CRYPT, MONSTER_CRYPT });
					target_loc.SetKnown();
					@target_loc.active_quest = quest;
					target_loc.AddEventHandler(quest, EVENT_ENTER);
					st = target_loc.st;
					
					target.AddDialog(quest, "end");
					target.temporary = false;
					
					quest.SetStarted(TEXT(0));
					quest.AddEntry(TEXT(1));
					quest.AddEntry(TEXT(2));
					quest.SetTimeout(60);
				}
				break;
			case P_FINDART_FINISHED:
				{
					int reward = GetReward();
					Team::AddReward(reward, reward * 3);
					
					target.AddItem(item);
					pc.unit.RemoveItem(item);
					target.temporary = true;
					
					if(target_loc.active_quest is quest)
						@target_loc.active_quest = null;
					
					quest.AddEntry(TEXT(3));
					quest.SetCompleted();
				}
				break;
			case P_FINDART_TIMEOUT:
				{
					target.temporary = true;
						
					if(target_loc.active_quest is quest)
						@target_loc.active_quest = null;
					
					quest.AddEntry(TEXT(4));
					quest.SetFailed();
				}
				break;
			}
		}
		
		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_ENTER)
			{
				if(Level::dungeon_level == target_loc.levels - 1)
				{
					e.location.RemoveEventHandler(quest);
					Chest@ chest = Level::GetTreasureChest();
					chest.AddItem(item);
					spawned = true;
				}
			}
			else if(e.event == EVENT_TIMEOUT)
			{
				if(spawned)
					target_loc.GetArea(target_loc.levels - 1).RemoveItemFromChest(item);
				else
					target_loc.RemoveEventHandler(quest);
				@target_loc.active_quest = null;
				quest.AddEntry(TEXT(5));
				progress = P_FINDART_ON_TIMEOUT;
			}
		}
		
		int GetReward()
		{
			return Quest::CalculateReward(st, Int2(5, 15), Int2(2000, 6000));
		}
		
		void OnUpgrade(Vars@ vars)
		{
			@item = vars["item"];
			@start_loc = vars["start_loc"];
			@target_loc = vars["target_loc"];
			int start_time = vars["start_time"];
			st = vars["st"];
			spawned = vars["done"];
			
			if(progress != P_FINDART_NONE)
				@item = item.QuestCopy(quest);

			if(quest.state != Q_STARTED)
				return;
			
			start_loc.FindQuestUnit(quest).AddDialog(quest, "end");
			
			int days = 60 + start_time - World::worldtime;
			if(days > 0)
				quest.SetTimeout(days);
			else if(progress == P_FINDART_STARTED)
				progress = P_FINDART_ON_TIMEOUT;
			
			if(progress == P_FINDART_STARTED)
			{
				if(!spawned)
					target_loc.AddEventHandler(quest, EVENT_ENTER);
				@target_loc.active_quest = quest;
			}
		}
	}
}

//=============================================================================
// ALCHEMIST APPRENTICE - unique quest given by alchemist
//=============================================================================
quest alchemist_apprentice
{
	type unique
	progress {
		P_ALCH_NONE
		P_ALCH_ACCEPTED
		P_ALCH_JOINED
		P_ALCH_REFUSED
		P_ALCH_ALL_PICKED
		P_ALCH_ALL_PICKED_AND_TALKED
		P_ALCH_ALL_PICKED_MANUALLY
		P_ALCH_FINISHED
		P_ALCH_FAILED
	}

	//-------------------------------------
	// dialogs
	dialog alchemist {
		if quest_progress between P_ALCH_ACCEPTED and P_ALCH_ALL_PICKED_MANUALLY
		{
			if once
			{
				talk 11
				do_once
			}
			if quest_progress between P_ALCH_ALL_PICKED and P_ALCH_ALL_PICKED_MANUALLY
			{
				if script "Team::HaveItem(herb)"
				{
					choice 12
					{
						set_quest_progress P_ALCH_FINISHED
						talk 14
						if script "alive"
						{
							talk 16
							script "pc.unit.AddTeamItem(Item::Get(\"p_hp2\"), 5);"
						}
						else
							talk 17
						restart
					}
				}
				else
				{
					choice 13
					{
						set_quest_progress P_ALCH_FAILED
						talk 15
						restart
					}
				}
			}
		}
		else
		{
			if once
			{
				talk 0
				do_once
			}
			choice 1
			{
				talk 2
				talk 3
				talk 4
				choice 5
				{
					talk 7
					set_quest_progress P_ALCH_ACCEPTED
					talk 8
					talk 9
					talk 10
					restart
				}
				choice 6
				{
					talk 18
					restart
				}
				show_choices
			}
		}
		end
	}

	dialog xwokloiz {
		if quest_progress P_ALCH_ACCEPTED
		{
			script "target.known_name = true;"
			talk 0
			talk 1
			talk 2
			choice 3
			{
				set_quest_progress P_ALCH_JOINED
				talk 5
				end
			}
			choice 4
			{
				set_quest_progress P_ALCH_REFUSED
				talk 6
				end
			}
			show_choices
		}
		else if quest_progress P_ALCH_REFUSED
		{
			talk 7
			choice 3
			{
				set_quest_progress P_ALCH_JOINED
				talk 5
				end
			}
			choice 4
			{
				talk 6
				end
			}
			show_choices
		}
		else if quest_progress P_ALCH_JOINED
		{
			talk 8
			end
		}
		else if quest_progress P_ALCH_ALL_PICKED_MANUALLY
		{
			talk 9
			end
		}
		else if quest_progress P_ALCH_ALL_PICKED
		{
			talk 10
			set_quest_progress P_ALCH_ALL_PICKED_AND_TALKED
			talk 11
			talk 12
			end
		}
		else
		{
			talk 12
			end
		}
	}

	//-------------------------------------
	// quest code
	code {
		Location@ city;
		Location@ forest;
		Item@ herb;
		GroundItem@ last_item;
		Vec3 last_pos;
		bool alive;

		void Startup()
		{
			@city = World::GetRandomSettlementWithBuilding("alchemist");
			city.AddEventHandler(quest, EVENT_ENTER);
			@herb = Item::Get("alchemist_herb");
			quest.AddRumor(TEXT(11));
			DevInfo(Format("Quest 'Alchemist apprentice' - {0}.", city.name));
			alive = true;
		}

		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_ENTER)
			{
				e.location.RemoveEventHandler(quest);
				if(progress == P_ALCH_NONE)
				{
					Unit@ u = Level::FindUnit(UnitData::Get("alchemist"));
					u.AddDialog(quest, "alchemist");
				}
				else if(progress == P_ALCH_ACCEPTED)
				{
					Level::SpawnItemRandomly(herb, Random(5, 7));
					Unit@ enemy = Level::GetNearestEnemy(Team::leader);
					Unit@ u = Level::SpawnUnitNearLocation(UnitData::Get("xwokloiz"), enemy.pos, 5);
					u.OrderEscapeToUnit(Team::leader)
						.ThenAutoTalk();
					u.AddEventHandler(quest, EVENT_DIE);
					forest.AddEventHandler(quest, EVENT_PICKUP);
					quest.RemoveRumor();
				}
			}
			else if(e.event == EVENT_UPDATE)
			{
				if(last_item is null)
				{
					@last_item = Level::FindNearestItem(herb, e.unit.pos);
					if(last_item !is null)
					{
						// move to next herb
						e.unit.OrderMove(last_item.pos)
							.WithMoveType(MOVE_RUN_WHEN_NEAR_TEAM);
					}
				}
				else if(last_item.pos != last_pos)
				{
					float dist = Vec3::Distance(e.unit.pos, last_item.pos);
					if(dist < 2)
					{
						e.unit.Talk(TEXT(9), 2);
						e.unit.OrderLookAt(last_item.pos)
							.WithTimer(1);
						last_pos = last_item.pos;
					}
				}
			}
			else if(e.event == EVENT_PICKUP)
			{
				if(e.item.base is herb)
				{
					if(last_item is e.item)
						@last_item = null;
					GroundItem@ item = Level::FindItem(herb);
					if(item is null)
					{
						// all herbs picked
						Unit@ u = Level::FindUnit(UnitData::Get("xwokloiz"));
						if(progress == P_ALCH_JOINED)
						{
							progress = P_ALCH_ALL_PICKED;
							u.OrderAutoTalk();
							u.RemoveEventHandler(quest);
						}
						else
						{
							progress = P_ALCH_ALL_PICKED_MANUALLY;
							u.OrderLeave();
						}
						quest.AddEntry(TEXT(6));
						forest.RemoveEventHandler(quest);
					}
				}
			}
			else if(e.event == EVENT_DIE)
			{
				e.unit.RemoveEventHandler(quest);
				alive = false;
				quest.AddEntry(TEXT(10));
			}
		}

		void SetProgress(int prev_progress)
		{
			switch(progress)
			{
			case P_ALCH_ACCEPTED:
				@forest = World::GetClosestLocation(L_OUTSIDE, city.pos, FOREST);
				forest.st = 9;
				forest.reset = true;
				@forest.active_quest = quest;
				forest.AddEventHandler(quest, EVENT_ENTER);
				forest.SetKnown();
				quest.SetStarted(TEXT(0));
				quest.AddEntry(TEXT(1));
				quest.AddEntry(TEXT(2));
				break;
			case P_ALCH_REFUSED:
				quest.AddEntry(TEXT(3));
				target.OrderClear();
				break;
			case P_ALCH_JOINED:
				if(prev_progress == P_ALCH_REFUSED)
					quest.AddEntry(TEXT(5));
				else
					quest.AddEntry(TEXT(4));
				target.AddEventHandler(quest, EVENT_UPDATE);
				target.OrderClear();
				break;
			case P_ALCH_ALL_PICKED_AND_TALKED:
				pc.unit.AddTeamItem(herb);
				target.RemoveEventHandler(quest);
				target.RemoveItem(herb);
				target.OrderLeave();
				break;
			case P_ALCH_FINISHED:
				{
					quest.AddEntry(TEXT(7));
					quest.SetCompleted();
					uint count = Team::RemoveItem(herb, 0);
					Team::AddReward(500*count, 7500);
					@forest.active_quest = null;
				}
				break;
			case P_ALCH_FAILED:
				quest.AddEntry(TEXT(8));
				quest.SetFailed();
				@forest.active_quest = null;
				break;
			}
		}
	}
}

//=============================================================================
// MAIN STORY QUEST
//=============================================================================
quest main
{
	type unique
	flags dont_count
	progress {
		P_MAIN_NONE
		P_MAIN_TALKED_TRAINER
		P_MAIN_START
		P_MAIN_TALKED_SOLTYS
		P_MAIN_ATTACKED
		P_MAIN_TALKED_SOLTYS2
		P_MAIN_TALKED_CLERIC
		P_MAIN_TALKED_CLERIC_HERO
		P_MAIN_TALKED_CLERIC_HERO2
		P_MAIN_CLERIC_RETURNED
		P_MAIN_CLERIC_RETURNED_TALKED
	}

	dialog trainer {
		if quest_progress == P_MAIN_NONE
		{
			talk 0
			talk 1
			set_quest_progress P_MAIN_TALKED_TRAINER
			end
		}
		talk 2
		choice 3
		{
			if script "Team::size < 4"
			{
				talk 6
				special "select_hero"
				escape choice 7
					end
				show_choices
			}
			else
			{
				talk 5
				end
			}
		}
		escape choice 4
			end
		show_choices
	}

	dialog messenger {
		if quest_progress == P_MAIN_TALKED_TRAINER
		{
			talk 0
			talk 1
			set_quest_progress P_MAIN_START
			talk 2
			talk 3
			talk 4
			talk 5
			end
		}
		talk 6
		end
	}

	dialog soltys {
		if once
			talk 0
		choice 1
		{
			switch quest_progress
			{
			case P_MAIN_START
				talk 2
				talk 3
				set_quest_progress P_MAIN_TALKED_SOLTYS
				talk 4
			case P_MAIN_TALKED_SOLTYS
				talk 5
			case P_MAIN_ATTACKED
				talk 6
				talk 7
				set_quest_progress P_MAIN_TALKED_SOLTYS2
				talk 8
				talk 9
			case default
				talk 10
			}
			restart
		}
		end
	}

	dialog ill_villager {
		talk 0
		if quest_progress == P_MAIN_TALKED_SOLTYS
		if script "!target.vars.IsSet(\"talked\")"
		{
			choice 1
			{
				script "counter++; Dialog::var = counter; target.vars[\"talked\"] = true;"
				switch var
				{
				case 1
					talk 3
				case 2
					talk 4
				case 3
					talk 5
					talk 6
				case 4
					talk 7
					talk 8
				case 5
					talk 9
					set_quest_progress P_MAIN_ATTACKED
				}
				end
			}
			escape choice 2
				end
			show_choices
		}
		end
	}

	dialog cleric_seller {
		choice 0
		{
			talk 1
			set_quest_progress P_MAIN_TALKED_CLERIC
			talk 2
			talk 3
			talk 4
			restart
		}
		end
	}
	
	dialog cleric {
		if quest_progress == P_MAIN_TALKED_CLERIC
		{
			talk 0
			talk 1
			talk 2
			set_quest_progress P_MAIN_TALKED_CLERIC_HERO
			talk 3
			restart
		}
		if quest_progress == P_MAIN_TALKED_CLERIC_HERO
		{
			talk 4
			talk 5
			set_quest_progress P_MAIN_TALKED_CLERIC_HERO2
			talk 6
			end2
		}
		if quest_progress >= P_MAIN_TALKED_CLERIC_HERO2
		{
			if quest_progress == P_MAIN_TALKED_CLERIC_HERO2
				talk 7
			if quest_progress == P_MAIN_CLERIC_RETURNED
			{
				talk 9
				talk 10
			}
			if choices > 0
			{
				escape choice 8
					end2
				show_choices
			}
			end2
		}
		end
	}

	code {
		Location@ village;
		Location@ shrine_loc;
		Location@ dungeon;
		int counter;
		Unit@ cleric;

		void Startup()
		{
			Vec2 pos = World::FindPlace(World::size/2, 64, true);
			Location@ academy = World::CreateLocation(L_OUTSIDE, pos, ACADEMY);
			@academy.active_quest = quest;
			@academy.group = UnitGroup::empty;
			academy.AddEventHandler(quest, EVENT_ENTER);
			World::SetStartLocation(academy);
		}

		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_ENTER)
			{
				if(Level::location is dungeon)
				{
					if(Level::dungeon_level == 0)
					{
						// find room for cleric
						array<Room@>@ path = Level::FindPath(Level::GetRoom(ROOM_ENTRY_PREV), Level::GetRoom(ROOM_ENTRY_NEXT));
						uint target_depth = 0;
						for(uint len = path.length(), i = len*2/3; i<len; ++i)
						{
							if(path[i].target != ROOM_CORRIDOR)
							{
								target_depth = i;
								break;
							}
						}
						Room@ target_room = path[target_depth];
						
						// kill units on path
						Unit@ player = Team::leader;
						for(uint i=0; i<target_depth; ++i)
						{
							array<Unit@>@ units = Level::GetUnits(path[i]);
							for(uint j=0; j<units.length(); ++j)
							{
								Unit@ u = units[j];
								if(u.IsEnemy(player) && Rand() % 2 == 0)
									units[j].Kill();
							}
						}
						array<Unit@>@ units = Level::GetUnits(target_room);
						for(uint j=0; j<units.length(); ++j)
						{
							Unit@ u = units[j];
							if(u.IsEnemy(player))
								units[j].Kill();
						}
						
						// spawn dead guards
						UnitData@ guard = UnitData::Get("guard");
						Unit@ u = Level::SpawnUnit(target_room, guard);
						u.Kill();
						@u = Level::SpawnUnit(path[target_depth == 0 ? 0 : target_depth - 1], guard);
						u.Kill();
						
						// spawn cleric
						Vec3 pos = Level::FindSpawnPos(target_room, cleric);
						cleric.MoveToArea(Level::location.area, pos);
						cleric.AddDialog(quest, "cleric");
						cleric.dont_attack = true;
						cleric.OrderAutoTalk();
					}
					else
					{
						Item@ book = Item::Get("book_q_cleric");
						Room@ room = Level::GetRoom(ROOM_TREASURY);
						Object@ obj = Level::FindObject(room, BaseObject::Get("book_holder"));
						Level::SpawnItem(book, obj);
						e.location.RemoveEventHandler(quest, EVENT_ENTER);
					}
				}
				else
				{
					e.location.RemoveEventHandler(quest, EVENT_ENTER);
					switch(progress)
					{
					case P_MAIN_NONE:
						{
							Unit@ u = Level::FindUnit(UnitData::Get("q_main_academy"));
							Team::Warp(u.pos - Vec3(0,0,1.5), u.pos);
							if(!Cutscene::ShouldSkip())
							{
								Cutscene::Start();
								Cutscene::Image("cutscene_plague.jpg", 23);
								Cutscene::Image("cutscene_academy.jpg", 11);
								for(int i=0; i<6; ++i)
									Cutscene::Text(TEXT(i), 5);
								Cutscene::End();
							}
							u.OrderAutoTalk(true);
						}
						break;
					case P_MAIN_START:
						{
							Unit@ u = Level::FindUnit(UnitData::Get("soltys"));
							u.AddDialog(quest, "soltys");

							UnitData@ ill = UnitData::Get("q_main_ill_villager");
							BuildingGroup@ group = BuildingGroup::Get("cottage");
							for(int i=0; i<5; ++i)
							{
								CityBuilding@ building = Level::GetRandomBuilding(group);
								Unit@ spawned = Level::SpawnUnitNearLocation(ill, building.unit_pos);
								spawned.RotateTo(building.unit_rot);
							}
						}
						break;
					case P_MAIN_TALKED_SOLTYS2:
						{
							Unit@ u = Level::FindUnit(UnitData::Get("cleric_seller"));
							u.AddDialog(quest, "cleric_seller");
						}
						break;
					}
				}
			}
			else if(e.event == EVENT_UPDATE)
			{
				if(Level::location is shrine_loc)
				{
					Unit@ unit = Level::FindUnit(UnitData::Get("cleric_seller"));
					if(Vec3::Distance(e.unit.pos, unit.pos) < 10)
					{
						progress = P_MAIN_CLERIC_RETURNED;
						Team::RemoveMember(e.unit);
						e.unit.OrderMove(unit.pos)
							.WithRange(2)
							.ThenAutoTalk();
						e.unit.RemoveEventHandler(quest);
					}
				}
			}
			else if(e.event == EVENT_GENERATE)
			{
				if(Level::dungeon_level == 0 && e.stage == 0)
				{
					e.map_settings.prev_entry_loc = ENTRY_BORDER;
					e.map_settings.next_entry_loc = ENTRY_FAR_FROM_PREV;
				}
				else if(Level::dungeon_level == 1 && e.stage == 1)
				{
					Room@ room = Level::GetRoom(ROOM_TREASURY);
					@room.type = RoomType::Get("crypt_library");
					Level::location.RemoveEventHandler(quest, EVENT_GENERATE);
				}
			}
		}

		void SetProgress()
		{
			switch(progress)
			{
			case P_MAIN_TALKED_TRAINER:
				{
					Unit@ u = Level::SpawnUnitNearLocation(UnitData::Get("q_main_messenger"), Team::leader.pos, 4);
					u.RotateTo(Team::leader.pos);
					u.OrderAutoTalk(true);
				}
				break;
			case P_MAIN_START:
				{
					@village = World::GetClosestLocation(L_CITY, World::pos, VILLAGE);
					village.AddEventHandler(quest, EVENT_ENTER);
					target.OrderLeave();
					quest.SetStarted(TEXT(6));
					quest.AddEntry(TEXT(7));
					quest.AddEntry(TEXT(8));
				}
				break;
			case P_MAIN_TALKED_SOLTYS:
				quest.AddEntry(TEXT(9));
				counter = 0;
				break;
			case P_MAIN_ATTACKED:
				quest.AddEntry(TEXT(10));
				target.ChangeBase(UnitData::Get("q_main_ill_villager_attack"));
				break;
			case P_MAIN_TALKED_SOLTYS2:
				@shrine_loc = World::GetRandomSettlementWithBuilding("shrine");
				shrine_loc.AddEventHandler(quest, EVENT_ENTER);
				quest.AddEntry(TEXT(11));
				break;
			case P_MAIN_TALKED_CLERIC:
				{
					target.RemoveDialog(quest);
					
					@cleric = World::CreateUnit(UnitData::Get("hero_cleric"), 8);
					
					Vec2 pos = World::FindPlace(Level::location.pos, 80, 100);
					@dungeon = World::CreateLocation(L_DUNGEON, pos, HERO_CRYPT, 2);
					@dungeon.active_quest = quest;
					@dungeon.group = UnitGroup::Get("undead");
					dungeon.st = 8;
					dungeon.SetKnown();
					dungeon.AddEventHandler(quest, EVENT_GENERATE);
					dungeon.AddEventHandler(quest, EVENT_ENTER);
					
					quest.AddEntry(TEXT(12));
				}
				break;
			case P_MAIN_TALKED_CLERIC_HERO:
				{
					target.dont_attack = false;
					target.known_name = true;
					target.AddEventHandler(quest, EVENT_UPDATE);
					Team::AddMember(target, 1);
					quest.AddEntry(TEXT(13));
				}
				break;
			case P_MAIN_TALKED_CLERIC_HERO2:
				{
					Vars vars;
					vars["cleric"] = cleric;
					vars["city"] = shrine_loc;
					Quest::Find("side_cleric").Start(vars);
				}
				break;
			}
		}
	}
}

//=============================================================================
// SIDE QUEST - cleric
//=============================================================================
quest side_cleric
{
	type unique
	progress {
		P_CLERIC_NONE
		P_CLERIC_START
		P_CLERIC_PICKED
		P_CLERIC_END
	}
	
	dialog end {
		if have_item book_q_cleric
			choice 0
			{
				talk 1
				set_quest_progress P_CLERIC_END
				talk 2
				end2
			}
		end
	}
	
	code {
		Unit@ cleric;
		Location@ city;
		Location@ dungeon;
		Item@ book;
		
		void Startup(Vars@ vars)
		{
			@cleric = vars["cleric"];
			@city = vars["city"];
			progress = P_CLERIC_START;
		}
		
		void SetProgress()
		{
			switch(progress)
			{
			case P_CLERIC_START:
				@book = Item::Get("book_q_cleric");
				@dungeon = Level::location;
				quest.SetStarted(TEXT(0));
				quest.AddEntry(TEXT(1));
				quest.AddEntry(TEXT(2));
				target.AddDialog(quest, "end", 1);
				@Level::location.active_quest = quest;
				Level::location.AddEventHandler(quest, EVENT_PICKUP);
				break;
			case P_CLERIC_PICKED:
				quest.AddEntry(TEXT(3));
				@Level::location.active_quest = null;
				break;
			case P_CLERIC_END:
				quest.AddEntry(TEXT(4));
				quest.SetCompleted();
				pc.unit.RemoveItem(book);
				Team::AddReward(4000, 12000);
				break;
			}
		}
		
		void OnEvent(Event@ e)
		{
			if(e.event == EVENT_PICKUP && e.item.base is book)
			{
				progress = P_CLERIC_PICKED;
				Level::location.RemoveEventHandler(quest);
			}
		}
	}
}

//=============================================================================
// UNIQUE HUNTERS QUEST - kill boss wolf
//=============================================================================
quest dire_wolf
{
	type unique
	flags not_scripted
	
	progress {
		P_WOLF_NONE
		P_WOLF_STARTED
		P_WOLF_KILLED
		P_WOLF_COMPLETE
	}
	
	properties {
		Location@ forest;
	}
	
	dialog hunter {
		if quest_progress == P_WOLF_NONE
		{
			choice 0
			{
				set_quest_progress P_WOLF_STARTED
				talk 1
				talk 2
				talk 3
				end
			}
		}
		else if quest_progress == P_WOLF_KILLED
		{
			set_quest_progress P_WOLF_COMPLETE
			do_once
			talk 4
			end
		}
		end
	}
}

//=============================================================================
// QUEST LISTS
//=============================================================================
quest_list mayor {
	deliver_letter 3
	deliver_parcel 3
	deliver_resources 3
	spread_news 2
	retrieve_package 2
	none 2
}

quest_list captain {
	rescue_captive 2
	bandits_collect_toll 2
	camp_near_city 2
	kill_animals 2
	wanted 2
	none 1
}

quest_list random {
	find_artifact 1
	lost_artifact 1
	stolen_artifact 1
}
